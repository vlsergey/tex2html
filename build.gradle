plugins {
	id 'antlr'
	id 'java-library'
	id 'maven-publish'
	id 'net.researchgate.release' version '2.8.1'
}

group 'com.github.vlsergey'

ext {
	lombokVersion = '1.18.12'
	queryDslVersion = '4.4.0'
	springDataVersion = '2.5.4'
}

compileJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	if (JavaVersion.current().isJava11Compatible()) {
		options.release = 8
	}

	options.compilerArgs << '-parameters'
	options.debugOptions.debugLevel = "source,lines,vars"
	options.encoding = 'UTF-8'
}

compileTestJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	if (JavaVersion.current().isJava11Compatible()) {
		options.release = 8
	}

	options.compilerArgs << '-parameters'
	options.debugOptions.debugLevel = "source,lines,vars"
	options.encoding = 'UTF-8'
}

repositories {
	mavenCentral()
}

dependencies {
	antlr group: "org.antlr", name: "antlr4", version: "4.5"

	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion

	implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
	implementation group: 'info.picocli', name:'picocli-spring-boot-starter', version: '4.6.2'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.5.6'

	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testAnnotationProcessor group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

	testCompileOnly group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

	testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
	testImplementation(platform('org.junit:junit-bom:5.7.2'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.5.4'
}

generateGrammarSource {
	outputDirectory = file("build/generated-src/antlr/com/github/vlsergey/tex2html/grammar")
	arguments += ["-package", "com.github.vlsergey.tex2html.grammar"]
}
sourceSets.main.java.srcDir "build/generated-src/antlr/"

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

//task regenerateReadme {
//	doLast {
//		def releaseVersion = project.property('release.releaseVersion');
//		copy {
//			from file( 'README.template.md')
//			rename 'README\\.template\\.md', 'README.md'
//			into project.projectDir
//			expand([ "version": releaseVersion ])
//		}
//
//		exec {
//			workingDir project.projectDir
//			commandLine 'git'
//			args 'add', 'README.md'
//		}
//
//		exec {
//			workingDir project.projectDir
//			commandLine 'git'
//			args 'commit', '-m', 'Update version in README.md to ' + releaseVersion
//		}
//	}
//}
//beforeReleaseBuild.dependsOn regenerateReadme

release {
	failOnCommitNeeded = true
	failOnSnapshotDependencies = true
	failOnUnversionedFiles = true
	failOnUpdateNeeded = true
	pushReleaseVersionBranch = false
}
